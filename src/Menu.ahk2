/************************************************************************
 * @description Tray icon menu
 * @file Menu.ahk2
 * @author © 2025 David G. Dahlstrom
 * @date 2025/07/30
 * @version 1.0.0.0
 ***********************************************************************/

/***************************************************************************
* LICENSE
* The code in this file is distributed freely on GitHub for hobbyist 
* users, and may be used and modified freely for personal, non-commercial 
* use. It may not be sold or distributed with any other product or software 
* without permission.
****************************************************************************/

#Requires AutoHotkey v2.0

class TrayMenu
{
	static oTrayMenu := A_TrayMenu

	/**
	 * Build the tray menu
	 */
	static BuildMenu()
	{
		TrayMenu.oTrayMenu.Delete() ;Delete standard items

		TrayMenu.oTrayMenu.Add("New", TrayMenu.ActionList)
		TrayMenu.oTrayMenu.Add()
		TrayMenu.oTrayMenu.Add("&Settings...", TrayMenu.Settings)
		TrayMenu.oTrayMenu.Add()
		TrayMenu.oTrayMenu.Add("&Help...", TrayMenu.Help)
		TrayMenu.oTrayMenu.Add("&About...", TrayMenu.About)
		TrayMenu.oTrayMenu.Add()
		TrayMenu.oTrayMenu.Add("E&xit", TrayMenu.FileExit)
	}

	static Help(*)
	{
		Run("notepad readme.txt", A_ScriptDir)
	}

	static ActionList(*)
	{

	}

	/**
	 * The "Settings..." menu item
	 */
	static Settings(*)
	{
		global bCancelPoll := true

		Settings.ClearTable()
		Settings.ReadActionList()

		gcDevices.Value := 1
		gcAnnunciationEdit.Text := ""
		gcProfileEdit.Text := ""
		gcProfileEdit_2.Text := ""
		gcProfile_2_Checkbox := 0
		Settings.ToggleCheckedState(0)

		AppLog.LogLine("INFO: Showing Settings dialog.`n")
		guiSettings.Show("X" Settings.mMetrics.X " Y" Settings.mMetrics.Y " W" Settings.mMetrics.Width " H" Settings.mMetrics.Height)
	}

	/**
	 * The "About..." box menu item
	 */
	static About(*)
	{
		sAboutTitle := "About BGPS"

		mAbout := Map()
		mAbout.Width := 250
		mAbout.Height := 100
		mAbout.X := Round((A_ScreenWidth / 2) - (mAbout.Width / 2))
		mAbout.Y := Round((A_ScreenHeight / 2) - (mAbout.Height / 2))

		guiAbout := Gui("-MaximizeBox -MinimizeBox", sAboutTitle) 
		guiAbout.Add("Text",, "Background Game Profile Switcher (BGPS).`n`n© 2025 David G. Dahlstrom")
		gcAboutExitButton := guiAbout.AddButton("Default W" 50, "OK")
		gcAboutExitButton.OnEvent("Click", About_Close)

		guiAbout.OnEvent("Close", About_Close)
		guiAbout.OnEvent("Escape", About_Close)

		gcAboutExitButton.GetPos(,, &w, &h)
		gcAboutExitButton.Move(Round(mAbout.Width/2 - w/2), mAbout.Height - h * 1.5)

		guiAbout.Show("X" mAbout.X " Y" mAbout.Y " W" mAbout.Width " H" mAbout.Height)

		About_Close(*)
		{
			guiAbout.Destroy()  ; Destroy the about box.
		}
	}

	static FileExit(*)
	{
		ExitApp()
	}

	static SetMenuName(sOldMenuItem, sNewMenuItem)
	{
		TrayMenu.oTrayMenu.Rename(sOldMenuItem, sNewMenuItem)
		TrayMenu.oTrayMenu.Check(sNewMenuItem)
	}
}